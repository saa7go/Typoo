' Gambas class file

' Typoo --------------------------------------------
' Small software to learn fast typing
' 
' Developers: --------------------------------------------------------
' Project Founder : Kusuma Negara <deny.krishna108@gmail.com>
' Programmer : Christian Kurniawan <saa7.go@gmail.com>
' Artwork Designer : Hamzah Fahrudin <karangdesaku@yahoo.co.id>
' Package Maintainer : Damar Riyadi <tldm217@gmail.com>
' License : GPL GNU General Public License Version 2
'
' coded in Gambas 2.22 on Kubuntu 11.10 machine
'
' ---------------------------------------------------------------------------------------
' "Diluar sana orang bisa berbagi "emas". Meski saya cuma bisa berbagi "kerikil",
' setidaknya hal itu tidak mengurangi semangat berbagi saya untuk teman - teman
' semua"
' 
' Semoga Aplikasi ini bisa bermanfaat untuk dunia pendidikan di Indonesia :)
' 
' 
' Thanks to:
' GAMBAS Creator
' ---------------------------------------------------------------------------------------

PRIVATE __error AS Integer
PRIVATE __type_count AS Integer
PRIVATE __time AS Integer
PRIVATE __start_time AS Date
PRIVATE __red_alert AS Boolean
PRIVATE __mulai_ngetik AS Boolean

PRIVATE CONST MaxRecentFiles AS Integer = 5
PRIVATE RecentFiles AS NEW String[]
PRIVATE RecentMenus AS NEW Object[]
PRIVATE FirstLoad AS Boolean = FALSE
PRIVATE start_type AS Boolean
PRIVATE stop_type AS Boolean
PRIVATE char AS String
PRIVATE __text AS String
PRIVATE pos AS Integer
PRIVATE correct_typing AS Integer
PRIVATE textlength AS Integer
PRIVATE cursorpos AS Integer

PRIVATE hfile AS File

PUBLIC SUB _new()
  hfile = OPEN User.Home &/ "typoo-dbg.txt" FOR OUTPUT CREATE 
   
  LoadConfig
  ME.Center
END

PUBLIC SUB caps_indicator(show_indicator AS Boolean)
  IF show_indicator = TRUE THEN 
    pict_caps_indicator__.Visible = TRUE
    lb_caps_indicator__.Visible = TRUE
  ELSE 
    pict_caps_indicator__.Visible = FALSE
    lb_caps_indicator__.Visible = FALSE
  ENDIF 
END

PUBLIC SUB capslock_info()
  IF mdlExtern.CapsLockIsOn() THEN 
    caps_indicator(TRUE)
  ELSE 
    caps_indicator(FALSE)
  ENDIF 
END

PUBLIC SUB type_here___KeyPress()
  DIM tmpcursorpos AS Integer = cursorpos
  DIM tmpchar AS String
  
  'Case sensitive key code still not supported by Gambas
  IF start_type = FALSE THEN 
    Message.Info("Silahkan klik tombol Mulai untuk memulai")
    STOP EVENT 
    RETURN 
  ENDIF 
  
  ' Block some key function
  IF mdlGlobal.func_array.Exist(Key.Code) THEN 
    type_here__.SetFocus
    STOP EVENT 
    RETURN 
  ENDIF 
  
  IF stop_type = FALSE THEN 
    IF Key.Code = Key.BackSpace THEN 
      STOP EVENT 
      RETURN 
    ENDIF 
  ENDIF 

  capslock_info
  
  char = Mid(__text, pos, 1)
  PRINT #hfile, "Current char: " & char & gb.NewLine
  IF char = gb.NewLine THEN 
    IF Key.Code = Key.Return OR Key.Code = Key.Enter THEN 
      correct_typing = TRUE
    ELSE 
      correct_typing = FALSE
    ENDIF 
  ELSE IF char = gb.Tab THEN 
    IF Key.Code = Key.Tab THEN 
      correct_typing = TRUE
    ELSE 
      correct_typing = FALSE
    ENDIF 
  ELSE IF Asc(char) >= 97 AND Asc(char) <= 122 THEN     ' letter a - z
    IF mdlExtern.CapsLockIsOn() THEN 
      correct_typing = FALSE
      'RETURN
      GOTO checktype
    ENDIF 
    IF Key.Code = Key[char] AND IF Key.Shift THEN
      correct_typing = FALSE
    ELSE IF Key.Code = Key[char] THEN 
      correct_typing = TRUE
    ELSE 
      correct_typing = FALSE
    ENDIF 
  ELSE IF Asc(char) >= 65 AND Asc(char) <= 90 THEN ' letter A - Z
    IF Key.Code = Key[char] AND IF Key.Shift THEN
      IF mdlExtern.CapsLockIsOn() = FALSE THEN
        correct_typing = TRUE
      ELSE 
        correct_typing = FALSE
      ENDIF 
    ELSE
      IF mdlExtern.CapsLockIsOn() THEN
        IF (Key.Code) = Key[char] THEN
          correct_typing = TRUE
        ELSE
          correct_typing = FALSE
        ENDIF
      ELSE
        correct_typing = FALSE
      ENDIF
    ENDIF
  ELSE IF Asc(char) >= 44 AND Asc(char) <= 57 THEN     ' number 0 - 9 dan some character
    IF (Key.Code) = Key[char] THEN
      correct_typing = TRUE
    ELSE
      correct_typing = FALSE
    ENDIF
  ELSE IF (Asc(char) = 32) OR (Asc(char) = 39) OR (Asc(char) = 59) OR (Asc(char) = 61) OR (Asc(char) = 91) OR (Asc(char) = 92) OR (Asc(char) = 93) OR (Asc(char) = 96) THEN ' spasi 59 61 91 92 93 96 
    IF (Key.Code) = Key[char] THEN
      correct_typing = TRUE
    ELSE
      correct_typing = FALSE
    ENDIF
  ELSE IF Asc(char) >= 33 AND Asc(char) <= 38 THEN ' huruf ! - +
    IF Key.Code = Key[char] AND IF Key.Shift THEN
      correct_typing = TRUE
    ELSE
      correct_typing = FALSE
    ENDIF   
  ELSE IF Asc(char) >= 40 AND Asc(char) <= 43 THEN ' huruf ! - +
    IF Key.Code = Key[char] AND IF Key.Shift THEN
      correct_typing = TRUE
    ELSE
      correct_typing = FALSE
    ENDIF   
  ELSE IF (Asc(char) = 58) OR (Asc(char) = 60) OR (Asc(char) = 62) OR (Asc(char) = 63) OR (Asc(char) = 64) OR (Asc(char) = 94) OR (Asc(char) = 95) OR (Asc(char) = 126) OR (Asc(char) = 123) OR (Asc(char) = 124) OR (Asc(char) = 125) THEN ' huruf ! - +
    IF Key.Code = Key[char] AND IF Key.Shift THEN
      correct_typing = TRUE
    ELSE
      correct_typing = FALSE
    ENDIF     
  ELSE
    RETURN
  ENDIF

checktype:
    IF stop_type = TRUE THEN 
      IF Key.Code = Key.BackSpace THEN 
        stop_type = FALSE
        RETURN 
      ELSE 
        stop_type = TRUE
        STOP EVENT 
        RETURN 
      ENDIF 
    ENDIF 

    IF correct_typing = TRUE THEN
    '// if correct typing then mark next letter with blue color
    IF pos <= textlength
      WITH board__
      cursorpos = cursorpos + 1
      .Select(cursorpos, 1)
      PRINT #hfile, "selection text: " & .Selection.Text & gb.NewLine
      WHILE Right(.Selection.Text, 6) = "<br />"
        .Unselect
        cursorpos = cursorpos + 1
        .Select(cursorpos, 1)
        PRINT #hfile, "selection text: " & .Selection.Text & gb.NewLine
      WEND 
      .Format.Color = Color.Blue 
      .Format.Font.Underline = TRUE
      .Unselect
      END WITH 
    ENDIF 
    
    '// if error typing, mark old letter with gray colors
    WITH board__
    .Select(tmpcursorpos, 1)
    IF __red_alert = FALSE THEN 
      .Format.Color = &FF6600&
    ELSE 
      .Format.Color = Color.Gray
    ENDIF 
    .Format.Font.Underline = FALSE
    .Unselect
    END WITH 
    
    '// go to next letter
    pos = pos + 1
    __type_count = __type_count + 1
    info_char__.Foreground = &HFFFF00&
    stop_type = FALSE
   __red_alert = FALSE
  ELSE     '// user type wrong letter :(
  
    '// if user press shift or capslock button don't record it as error :(
    IF Key.Code = 4128 THEN  'key shift
      stop_type = stop_type
      RETURN
    ENDIF
    IF Key.Code = 4132 THEN  'key capslock
      stop_type = stop_type
      capslock_info
      RETURN 
    ENDIF 
    
    '// don't record again if user have error before
    IF stop_type = FALSE THEN
      __error = __error + 1
    ENDIF
    
    'pos = pos
    stop_type = TRUE
    
    char_error__.Text = Format(__error, "#,##")
   
    '// show some error info with red color
    info_char__.Foreground = Color.Red
    WITH board__
    .Select(cursorpos, 1)
    .Format.Color = Color.Red 
    .Format.Font.Underline = TRUE
    .Unselect    
    END WITH 
    __red_alert = TRUE
  ENDIF 
  
  IF pos <= textlength THEN 
    '// show detail information until no letter to type
    tmpchar = Mid(__text, pos, 1)
    IF tmpchar = gb.NewLine THEN 
      tmpchar = "↵"
    ELSE IF tmpchar = gb.Tab THEN 
      tmpchar = "⇥"
    ENDIF 
    info_char__.Text = tmpchar
  ELSE 
      stop_type___Click
  ENDIF 

END

PUBLIC SUB mn_open_Click()
  OpenFileTutorial
END

PUBLIC SUB Board_Init()
  pos = 1
  __type_count = 0
  __error = 0
  
  WITH board__
  .SelectAll
  .Format.Color = Color.Black
  .Format.Font.Underline = FALSE
  .Format.Alignment = Align.Justify
  .Refresh
  .Select(cursorpos, 1)
  .Format.Color = Color.Blue
  .Format.Font.Underline = TRUE
  .Unselect
  END WITH 
  type_here__.SetFocus
END

PUBLIC SUB Board_First_Char()
  char = Mid(__text, pos, 1)
  char_error__.Caption = "0"
  info_char__.Caption = char
  info_char__.ForeColor = &HFFFF00&
  speed__.Caption = "0"
END

PUBLIC SUB Form_Activate()
  type_here__.SetFocus
END

PUBLIC SUB start_type___Click()

  type_here__.Text = ""
  
  IF textlength = 0 THEN 
    Message.Info("Silahkan buka berkas tutorial")
    RETURN 
  ENDIF 
  
  cursorpos = 0
  start_type = TRUE
  start_type__.Enabled = FALSE
  stop_type__.Enabled = TRUE
  mn_open.Enabled = FALSE
  null_info
  Board_Init
  Board_First_Char
  __time = 0
  Init_Timer
  
END

PUBLIC SUB Init_Timer()
  start_time__.Caption = Format(Time(Now), "hh:nn:ss")
  __start_time = Time(Now)
  stop_time__.Caption = "00:00:00"
  Timer1.Enabled = TRUE
END

PUBLIC SUB null_info()
  start_time__.Caption = "00:00:00"
  stop_time__.Caption = "00:00:00"
  info_char__.Caption = ""
  char_error__.Caption = "0"
  speed__.Caption = "0"
  char_accuracy__.Caption = "0 %"
END

PUBLIC SUB Form_Close()
  CLOSE #hfile
  Timer1.Enabled = FALSE
  FMain.Show
  SaveConfig
END

PUBLIC SUB stop_type___Click()

  start_type = FALSE
  start_type__.Enabled = TRUE
  stop_type__.Enabled = FALSE
  mn_open.Enabled = TRUE
  Timer1.Enabled = FALSE
  IF __type_count > 0 THEN 
    calculate_speed
    calculate_accuracy
    IF textlength = __type_count THEN 
      Message.Info("Selesai")
    ELSE 
      Message.Warning("diakhiri oleh pengguna")
    ENDIF 
  ENDIF 
END

PUBLIC SUB calculate_speed()
  DIM speed AS Integer
  
  stop_time__.Caption = Format(CDate(__start_time + Date(0, 0, 0, 0, 0, __time)), "hh:nn:ss")
  speed = __type_count / (__time / 60)
  speed__.Caption = speed
END

PUBLIC SUB calculate_accuracy()
  DIM accuracy AS Integer
  
  accuracy = ((__type_count - __error) / textlength) * 100
  char_accuracy__.Caption = accuracy & " %"
END

PUBLIC SUB Timer1_Timer()

__time = __time + 1

END

PUBLIC SUB mn_exit_Click()
  ME.Close
END

PUBLIC SUB mn_recent1_Click()

  IF FirstLoad = FALSE THEN
    OpenFileTutorial(RecentFiles[0])
    FirstLoad = TRUE
  ENDIF

END

PUBLIC SUB mn_recent2_Click()
  OpenFileTutorial(RecentFiles[1])
END

PUBLIC SUB mn_recent3_Click()
  OpenFileTutorial(RecentFiles[2])
END

PUBLIC SUB mn_recent4_Click()
  OpenFileTutorial(RecentFiles[3])
END

PUBLIC SUB mn_recent5_Click()
  OpenFileTutorial(RecentFiles[4])
END

PRIVATE SUB OpenFileTutorial(OPTIONAL filePath AS String = "")
  
  DIM texttemp AS String
  DIM iIndex AS Integer
    
  __text = ""
  texttemp = ""
  iIndex = -1 ' not in recent files
  
  IF filePath <> "" THEN
    IF Exist(filePath) = FALSE THEN
      Message.Warning("File not exist, please choose another file.", "OK")
      filePath = ""
    ENDIF
  ENDIF
  
  IF filePath = "" THEN
    Dialog.Title = "Select tutorial file..."
    Dialog.Filter = ["*", "Text File"]
    
    IF Exist(mdlGlobal.last_open_dir) = FALSE THEN
      mdlGlobal.last_open_dir = User.Home
    ENDIF
    
    Dialog.Path = mdlGlobal.last_open_dir
    IF Dialog.OpenFile(FALSE) THEN
      RETURN
    ENDIF
    
    filePath = Dialog.Path
  ENDIF
  
  IF RecentFiles.Exist(filePath) THEN
      iIndex = RecentFiles.Find(filePath)
      IF iIndex > 0 THEN
        RecentFiles.Remove(iIndex)
      ENDIF
  ENDIF
  
  IF iIndex = -1 AND RecentFiles.Count = 0 THEN
    RecentFiles.Add(filePath)
  ELSE IF (iIndex = -1 AND RecentFiles.Count > 0) OR iIndex > 0 THEN
    RecentFiles.Add(filePath, 0)
  ENDIF
  
  IF RecentFiles.Count > MaxRecentFiles THEN ' make sure RecentFiles.count is only 5
    RecentFiles.Pop()
  ENDIF
  
  IF iIndex = -1 OR iIndex > 0 THEN
    RearrangeSubMenuRecentFiles
  ENDIF
  
  IF mn_recentFiles.Enabled = FALSE THEN
    mn_recentFiles.Enabled = TRUE
  ENDIF
  
  mdlGlobal.last_open_dir = mdlGlobal.DirPath(filePath)
  __text = File.Load(filePath)
  textlength = Len(__text)
  texttemp = __text
  texttemp = Replace(texttemp, "<", "&#60 ")
  texttemp = Replace(texttemp, ">", "&#62 ")
  texttemp = Replace(texttemp, gb.NewLine, "↵<br/>")
  texttemp = Replace(texttemp, gb.Tab, "⇥")
  board__.Text = texttemp
  texttemp = ""
  char_count__.Text = Format(textlength, "#,##")
  
END


PRIVATE SUB LoadConfig()
  
  DIM filePath AS String
  DIM i AS Integer
  
  mdlGlobal.last_open_dir = Settings["Config/LastOpenDir", User.Home]
  IF Exist(mdlGlobal.last_open_dir) = FALSE THEN mdlGlobal.last_open_dir = User.Home
  
  FOR i = 1 TO MaxRecentFiles STEP 1
    filePath = Settings["RecentFiles/File" & i, ""]
    IF Len(filePath) > 0 THEN
      RecentFiles.Add(filePath)
    ELSE
      BREAK
    ENDIF
  NEXT
  
  RecentMenus.Add(mn_recent1)
  RecentMenus.Add(mn_recent2)
  RecentMenus.Add(mn_recent3)
  RecentMenus.Add(mn_recent4)
  RecentMenus.Add(mn_recent5)
  
  IF RecentFiles.Count > 0 THEN
    mn_recentFiles.Enabled = TRUE
    FOR i = 0 TO RecentFiles.Count - 1 STEP 1
      RecentMenus[i].Text = RecentFiles[i]
    NEXT
  ELSE
    mn_recentFiles.Enabled = FALSE
  ENDIF
  
  RearrangeSubMenuRecentFiles
  
END

PRIVATE SUB SaveConfig()
  
  DIM i AS Integer
  Settings["Config/LastOpenDir"] = mdlGlobal.last_open_dir
  FOR i = 1 TO RecentFiles.Count STEP 1
    Settings["RecentFiles/File" & i] = RecentFiles[i - 1]
  NEXT
  
END

PRIVATE FUNCTION RearrangeSubMenuRecentFiles()
  
  DIM i AS Integer
  FOR i = 0 TO RecentFiles.Count - 1 STEP 1
    RecentMenus[i].Text = RecentFiles[i]
    IF RecentMenus[i].Visible = FALSE THEN RecentMenus[i].Visible = TRUE
  NEXT
  
END
